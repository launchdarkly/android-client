apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'signing'
apply plugin: 'org.ajoberstar.github-pages'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'io.codearte.nexus-staging'

allprojects {
    group = 'com.launchdarkly'
    version = '2.9.1'
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xbootclasspath/a:" + System.properties.get("java.home") + "/lib/rt.jar"
    }
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName version
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-proguard-rules.pro'

        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    testOptions {
        execution 'ANDROID_TEST_ORCHESTRATOR'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/**'
        exclude 'META-INF/**'
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

}

ext {
    supportVersion = "26.0.1"
    okhttpVersion = "3.12.2"
    eventsourceVersion = "1.8.0"
    gsonVersion = "2.8.2"
    testRunnerVersion = "1.0.2"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.google.code.gson:gson:$gsonVersion"
    implementation "com.android.support:support-annotations:$supportVersion"
    implementation "com.launchdarkly:okhttp-eventsource:$eventsourceVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.jakewharton.timber:timber:4.7.1"
    // Explicitly old version of Google Play Services to allow compatibility without update for
    // older phones
    //noinspection GradleDependency
    implementation 'com.google.android.gms:play-services-base:6.5.87'

    androidTestUtil "com.android.support.test:orchestrator:$testRunnerVersion"
    androidTestImplementation "com.android.support:appcompat-v7:$supportVersion"
    androidTestImplementation "com.android.support:support-annotations:$supportVersion"
    androidTestImplementation "com.android.support.test:runner:$testRunnerVersion"
    androidTestImplementation "com.android.support.test:rules:$testRunnerVersion"
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    androidTestImplementation 'org.easymock:easymock:3.6'
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:$okhttpVersion"
}

repositories {
    mavenLocal()
    // Before LaunchDarkly release artifacts get synced to Maven Central they are here along with snapshots:
    maven { url "https://oss.sonatype.org/content/groups/public/" }
    mavenCentral()
    google()
}

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        google()
        jcenter()
    }
    dependencies {
        classpath 'org.ajoberstar:gradle-git:1.5.0-rc.1'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.2'
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.8.0"
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.0'
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

afterEvaluate {
// https://stackoverflow.com/questions/34571371/android-studio-javadoc-cannot-find-symbol/34572606#34572606
    javadoc.classpath += files(android.libraryVariants.collect { variant ->
        variant.javaCompile.classpath.files
    })
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

githubPages {
    repoUri = 'https://github.com/launchdarkly/android-client-sdk.git'
    pages {
        from javadoc
    }
    credentials {
        username = githubUser
        password = githubPassword
    }
}

signing {
    sign configurations.archives
}

nexusStaging {
    packageGroup = "com.launchdarkly"
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'LaunchDarkly SDK for Android'
                packaging 'jar'
                description 'Official LaunchDarkly SDK for Android'
                url 'https://github.com/launchdarkly/android-client-sdk'

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'drichelson'
                        name 'Dan Richelson'
                        email 'dan@launchdarkly.com'
                    }
                }

                scm {
                    connection 'scm:git:git://github.com/launchdarkly/android-client-sdk.git'
                    developerConnection 'scm:git:ssh:git@github.com:launchdarkly/android-client-sdk.git'
                    url 'https://github.com/launchdarkly/android-client-sdk'
                }
            }
        }
    }
}
